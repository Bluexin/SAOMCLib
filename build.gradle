buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}
apply plugin: "maven"
apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mod_version}"
group = "be.bluexin"
archivesBaseName = "saomc-lib"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"
    mappings = "${mappings_version}"

    replace "@VERSION@", project.version
    replaceIn "SAOMCLib.kt"
    useDepAts = true
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://maven.bluexin.be/repository/releases/"
    }
    maven {
        url "http://maven.shadowfacts.net/"
    }
}

dependencies {
    compile group: "net.shadowfacts", name: "Forgelin", version: forgelin_version
}

configurations {
    shade
    compile.extendsFrom shade
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea { module { inheritOutputDirs = true } }

uploadArchives {
    if (System.getenv('REPO_PWD') != null) {
        repositories.mavenDeployer {
            repository(url: "http://maven.bluexin.be/repository/" + (((String) project.version).contains("SNAPSHOT")? "snapshots": "releases") + "/") {
                authentication(userName: "CI", password: System.getenv('REPO_PWD'))
            }
        }
    } else {
        println("No password set up! Uploading to local.")
        repositories.mavenDeployer {
            repository(url: 'file://G:/saomclib/build/mvn')
        }
    }
}

task sourceJar(type: Jar, dependsOn: "sourceMainJava", overwrite: true) {
    from "src/main/java"
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives sourceJar
    archives deobfJar
}

jar {
    for (dep in configurations.shade) {
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

tasks.withType(Copy) {
    includeEmptyDirs = false
}
